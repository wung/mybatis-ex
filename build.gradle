apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing' //使用signing plugin做数字签名

group = 'com.github.penggle'
version = '1.0.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.versions = [
	mybatis: '3.2.7',
	mybatisSpring: '1.2.2',
	
	junit: '4.11',
	slf4j: '1.7.7',
	logback: '1.1.2',
	cglib: '3.1',
	spring: '3.2.10.RELEASE',
	commonsPool2: '2.2',
	commonsDbcp2: '2.0.1',
	mysql: '5.1.31'
]

[compileJava, javadoc, compileTestJava]*.options*.encoding = "UTF-8"

jar {
    manifest {
        attributes 'Implementation-Title': 'mybatis-ex', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
		"org.mybatis:mybatis:${versions.mybatis}",
		"org.mybatis:mybatis-spring:${versions.mybatisSpring}",
		"org.springframework:spring-core:${versions.spring}",
		"org.springframework:spring-beans:${versions.spring}",
		"org.springframework:spring-context:${versions.spring}",
		"org.springframework:spring-context-support:${versions.spring}",
		"org.springframework:spring-jdbc:${versions.spring}"
    )
	
    testCompile(
		"junit:junit:${versions.junit}",
		"org.slf4j:slf4j-api:${versions.slf4j}",
		"ch.qos.logback:logback-core:${versions.logback}",
		"ch.qos.logback:logback-classic:${versions.logback}",
		"mysql:mysql-connector-java:${versions.mysql}",
		"cglib:cglib-nodep:${versions.cglib}",
		"org.springframework:spring-aop:${versions.spring}",
		"org.springframework:spring-aspects:${versions.spring}",
		"org.springframework:spring-expression:${versions.spring}",
		"org.springframework:spring-tx:${versions.spring}",
		"org.springframework:spring-test:${versions.spring}",
		"org.apache.commons:commons-pool2:${versions.commonsPool2}",
		"org.apache.commons:commons-dbcp2:${versions.commonsDbcp2}"
	)
}

//参见Part 2， 为项目生成**.jar/**-javadoc.jar/**-sources.jar  
task javadocJar(type: Jar, dependsOn: javadoc) {  
    classifier = 'javadoc'  
    from 'build/docs/javadoc'  
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

//为所有的jar包做数字签名
signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			//为Pom文件做数字签名
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  
			//指定项目部署到的中央库地址，UserName和Password就是Part 1中注册的账号,
			/**
			 * 看jira中的comment,snapshot和release版本的提交地址是不一样的(注意版本号后面是否带-SNAPSHOT),否则报400
			 * Configuration has been prepared, now you can:
			 * 		Deploy snapshot artifacts into repository https://oss.sonatype.org/content/repositories/snapshots
			 * 		Deploy release artifacts into the staging repository https://oss.sonatype.org/service/local/staging/deploy/maven2
			 * 		Promote staged artifacts into repository 'Releases'
			 * 		Download snapshot and release artifacts from group https://oss.sonatype.org/content/groups/public
			 * 		Download snapshot, release and staged artifacts from staging group https://oss.sonatype.org/content/groups/staging
			 */
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
				authentication(userName: nexusUsername, password: nexusPassword)
			}
			//构造项目的Pom文件，参见Part 2中Pom文件的规范，不要遗漏必填项
			pom.project {
				name project.name
				packaging 'jar'
				description 'This project is an extension of mybatis by using dynamic executor bound with current thread to solve [Cannot change the ExecutorType when there is an existing transaction] for mybatis-spring project.'
				url 'https://github.com/penggle/mybatis-ex'
  
				scm {
					url 'scm:git@github.com:penggle/mybatis-ex.git'
					connection 'scm:git@github.com:penggle/mybatis-ex.git'
					developerConnection 'git@github.com:penggle/mybatis-ex.git'
				}
  
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
  
				developers {
					developer {
						id 'pengpeng'
						name 'Peng Peng'
					}
				}
			}
		}
	}
}